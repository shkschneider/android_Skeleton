apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.shkschneider'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 16 // Jelly Bean 4.1.x
        targetSdkVersion 26
        versionCode 41
        versionName '8.0.0'
        // setProperty('archivesBaseName', "skeleton-$versionName")
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
    }
    defaultPublishConfig 'release'
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }
    // publishNonDefault true
}

dependencies {
    compile "com.android.support:design:${versions.support}"
    compile "com.android.support:palette-v7:${versions.support}"
    compile "com.android.support:recyclerview-v7:${versions.support}"
    compile "com.android.support:preference-v14:${versions.support}"

    compile 'com.android.volley:volley:1.0.0'

    compile 'com.google.code.gson:gson:2.8.1'
}

task javadoc(type: Javadoc) {
    failOnError false
    title = 'Skeleton'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // classpath += configurations.compile
    destinationDir = file('build/docs/javadoc/')
    options {
        links 'http://docs.oracle.com/javase/8/docs/api/'
        linksOffline 'http://d.android.com/reference', "${android.sdkDirectory}/docs/reference"
        // overview = 'overview.html'
        memberLevel = JavadocMemberLevel.PACKAGE
    }
    include '**/me/shkschneider/skeleton/**'
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// uncomment to build a jar file in addition to the default aar file
//android.libraryVariants.all { variant ->
//    def name = variant.buildType.name
//    def task = project.tasks.create "jar${name.capitalize()}", Jar
//    task.dependsOn variant.javaCompile
//    task.from variant.javaCompile.destinationDir
//    artifacts.add('archives', task);
//}
