apply plugin: 'com.android.library'
apply plugin: 'android-maven'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 22
        versionName '4.2.2'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }
}

dependencies {
    compile 'com.google.android.gms:play-services-location:8.1.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.mcxiaoke.volley:library:1.0.19'

    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:palette-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'

    compile 'com.github.shkschneider:android_PreferenceFragmentCompat:1.0.0@aar'
    compile 'com.github.shkschneider:android_RuntimePermissionsCompat:1.0.0@aar'
}

task javadoc(type: Javadoc) {
    title = "Skeleton"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file('build/docs/javadoc/')
    options {
        links 'http://docs.oracle.com/javase/7/docs/api/'
        linksOffline 'http://d.android.com/reference', "${android.sdkDirectory}/docs/reference"
        // overview = 'overview.html'
        memberLevel = JavadocMemberLevel.PACKAGE
    }
    include '**/me/shkschneider/skeleton/**'
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task jar(type: Jar, dependsOn: 'assembleRelease') {
    from fileTree(dir: 'build/intermediates/classes/release')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

// uncomment to build a jar file in addition to the default aar file
//android.libraryVariants.all { variant ->
//    def name = variant.buildType.name
//    def task = project.tasks.create "jar${name.capitalize()}", Jar
//    task.dependsOn variant.javaCompile
//    task.from variant.javaCompile.destinationDir
//    artifacts.add('archives', task);
//}
