apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    Properties versionProperties = new Properties()
    def versionPropertiesFile = file('version.properties')
    if (versionPropertiesFile.exists()) versionProperties.load(new FileInputStream(versionPropertiesFile))
    def androidDefaultConfigVersionCode = (versionProperties['versionCode'] ?: "0").toInteger()

    defaultConfig {
        applicationId 'me.shkschneider.skeleton.demo'
        minSdkVersion 16 // Jelly Bean 4.1.x
        targetSdkVersion 28
        versionCode androidDefaultConfigVersionCode
        versionName "${versions.skeleton}"
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = applicationId + "-" + variant.productFlavors[0].name +
                        "_v" + defaultConfig.versionName + "r" + defaultConfig.versionCode +
                        "-" + variant.variantData.variantConfiguration.buildType.name + ".apk"
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            // keytool -genkey -v -keystore release.jks -keyalg RSA -keysize 2048 -validity 10000 -alias androidkey
            storeFile file('../release.keystore')
            storePassword 'android'
            keyAlias 'androidkey'
            keyPassword 'android'
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildTypes {
        debug {
            // applicationIdSuffix '.debug'
            // versionNameSuffix '-DEBUG'
            debuggable true
            jniDebuggable true
            zipAlignEnabled true
            multiDexEnabled true
            signingConfig signingConfigs.debug
            shrinkResources false
            buildConfigField "boolean", "RELEASE", "false"
        }
        release {
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
            multiDexEnabled true
            signingConfig signingConfigs.release
            buildConfigField "boolean", "RELEASE", "true"
        }
    }
    defaultPublishConfig 'debug'
    flavorDimensions 'default'
    productFlavors {
        alpha {
            dimension 'default'
            // versionNameSuffix '-ALPHA'
            resValue "string", "url", "http://android.com?alpha"
        }
        beta {
            dimension 'default'
            // versionNameSuffix '-BETA'
            resValue "string", "url", "http://android.com?beta"
        }
        prod {
            dimension 'default'
            resValue "string", "url", "http://android.com"
        }
    }
    bundle {
        /**
         * If the user changes their device configuration at any time (such as adding another choice of language),
         * then the play store will recognize this and attempt to download the new configuration splits
         * for all applications that use split APKs on their device.
         */
        language.enableSplit = true
        density.enableSplit = true
        abi.enableSplit = true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
}

task bump() << {
    print '> Bump...'
    Properties versionProperties = new Properties()
    def versionPropertiesFile = file('version.properties')
    if (!versionPropertiesFile.exists()) {
        throw new GradleException("Could not read version.properties")
    }
    versionProperties.load(new FileInputStream(versionPropertiesFile))
    versionProperties['versionCode'] = ((versionProperties['versionCode'] ?: "0").toInteger() + 1).toString()
    versionProperties.store(versionPropertiesFile.newWriter(), null)
    print " v${versions.skeleton}r" + versionProperties['versionCode'] + "\n"
}
tasks.whenTaskAdded { task ->
    if (task.name == 'preReleaseBuild') {
        task.dependsOn bump
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk${versions.java}:${versions.kotlin}"

    implementation "androidx.preference:preference:${versions.androidx}"
    implementation "androidx.core:core-ktx:${versions.androidx}"
    implementation "com.google.android.material:material:${versions.material}"

    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'

    implementation project(':library')
    // implementation 'com.github.shkschneider:android_Skeleton:${versions.skeleton}'
}
