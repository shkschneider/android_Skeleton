apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        applicationId 'me.shkschneider.skeleton.demo'
        minSdkVersion 14 // Ice Cream Sandwich 4.x.x
        targetSdkVersion 24
        versionCode 29
        versionName '5.4.1'
        jackOptions {
            enabled true
        }
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('../release.keystore')
            storePassword 'android'
            keyAlias 'androidkey'
            keyPassword 'android'
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }
    buildTypes {
        debug {
            // applicationIdSuffix '.dev'
            // versionNameSuffix '-DEV'
            debuggable true
            jniDebuggable false
            zipAlignEnabled true
            multiDexEnabled true
            signingConfig signingConfigs.debug
            minifyEnabled false

            buildConfigField "boolean", "MOCK", "true"
            resValue "string", "url", "http://android.com"
        }
        release {
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
            multiDexEnabled true
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            shrinkResources true

            buildConfigField "boolean", "MOCK", "false"
            resValue "string", "url", "http://android.com"
        }
    }
    splits {
        // universal needs a lower versionCode
        density {
            enable true
            reset()
            include 'mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi'
            compatibleScreens 'small', 'normal', 'large', 'xlarge'
        }
        // arm needs a lower versionCode
        abi {
            enable false
            reset()
            include 'x86', 'armeabi-v7a', 'mips'
            universalApk true
        }
    }
}

// assign different version code for each output
ext.versionCodes = ['mdpi':1, 'hdpi':2, 'xhdpi':3, 'xxhdpi':4, 'xxxhdpi':5].withDefault {0}
import com.android.build.OutputFile
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        output.versionCodeOverride = project.ext.versionCodes.get(output.getFilter(OutputFile.DENSITY)) * 1000000 + android.defaultConfig.versionCode
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'

    compile project(':library')
    // compile 'com.github.shkschneider:android_Skeleton:5.4.0'

    androidTestCompile 'com.android.support:support-annotations:24.2.0'
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2.2') { exclude group: 'support-annotations' }
}
