buildscript {
    ext.versions.android = [
        sdk: [
            target: 29,
            min   : 16 // Jelly Bean 4.1.x
        ],
        buildTools: '29.0.0'
    ]
}

subprojects {

    afterEvaluate { project ->

        def isAndroidApplication = project.plugins.findPlugin('com.android.application') != null
        def isAndroidLibrary = project.plugins.findPlugin('com.android.library') != null

        if (isAndroidApplication || isAndroidLibrary) {

            android {
                def defaultConfigVersionCode = versionCode()

                // Android
                compileSdkVersion versions.android.sdk.target
                // <https://developer.android.com/studio/releases/build-tools>
                buildToolsVersion versions.android.buildTools

                defaultConfig {
                    minSdkVersion versions.android.sdk.min
                    targetSdkVersion versions.android.sdk.target
                    versionCode defaultConfigVersionCode
                    versionName versions.skeleton
                }

                buildTypes {
                    debug {
                        debuggable true
                        jniDebuggable true
                        zipAlignEnabled true
                        multiDexEnabled true
                        shrinkResources false
                    }
                    release {
                        debuggable false
                        jniDebuggable false
                        zipAlignEnabled true
                        multiDexEnabled true
                        shrinkResources false // TODO
                    }
                }

                // Java 8
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                // Applications and Libraries
                if (isAndroidApplication) {
                    flavorDimensions 'default'
                    applicationVariants.all { variant ->
                        variant.outputs.all { output ->
                            outputFileName = "${applicationId}-${variant.productFlavors[0].name}" +
                                    "_v${defaultConfig.versionName}r${defaultConfigVersionCode}" +
                                    "-${variant.variantData.variantConfiguration.buildType.name}.apk"
                        }
                    }
                }
                if (isAndroidLibrary) resourcePrefix 'sk_'

                // Lint
                lintOptions {
                    checkReleaseBuilds true
                    abortOnError true
                }
            }

        }

    }

}
